GAL22V10
speechctl

CLK   MMAP  MDO   MDI   A0    A1    RESET READY INT   NC    NC    GND
NC    RSAUX IRST  SFRO  DATAR BUSY  SDRO  SDRA  SDRI  WS    RS    VCC


;
; Pin Logic
;

; TMS5220C Controller

; RSAUX is used to simplify the "group NOT" encoding
RSAUX = MMAP * MDO * /MDI * A0 * /A1 * CLK * /READY * /RESET

/WS =    MMAP * /MDO * MDI * /A0 * /A1 * CLK * /READY * RS * /RESET * /IRST
       + READY * /WS * RS * /RESET * /IRST

/RS =    MMAP * /MDO * MDI * A0 * /A1 * CLK * /READY * WS * RS * /RESET * /IRST
       + /RS * WS * /RSAUX * /IRST

; Register Control

/SDRI =  MMAP * /MDO * MDI * /A0 * /A1 * /READY * /RESET

/SDRA =  READY * /WS * RS * /RESET

/SDRO =   MMAP * MDO * /MDI * A0 * /A1 * /READY * WS * /RESET

/SFRO =   MMAP * MDO * /MDI * /A0 * A1 * /RESET

; Status

BUSY =    READY + /WS + /RS + RESET

DATAR =   /READY * WS * /RS * /RESET

IRST = MMAP * /MDO * MDI * A0 * /A1 

DESCRIPTION

Glue logic to connect TMS5220C to 8 bit data bus and controlled via memory map.