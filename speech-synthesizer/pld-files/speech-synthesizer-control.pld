GAL22V10
speechctl

CLK   MMAP  MDO   MDI   A0      A1      RESET    READY  INT    NC    NC    GND
OE    NC    NC    BUSY  STATEO  STATUSO STATUSI  DATAA  DATAI  WS    RS    VCC


; Memory Map Lines
;
;   Function | A1 | A0
;  ----------+----+----
;   DATA_IN  |  0 |  0
;   STATUS   |  0 |  1
;   STATE    |  1 |  0



;
; Pin Logic
;

; TMS5220C Controller
;
; Write Data for External Speech Data

/DATAI = MMAP * MDI * /MDO * /A1 * /A0 * /BUSY

/WS.R = /DATAI * WS + /WS * READY

/DATAA = /WS * READY

;
; TMS5220C Status Fetch Operation
;

/RS.R = MMAP * MDI * /MDO * /A1 * A0 * /BUSY * RS + /RS * READY

STATUSI = /READY * /RS * BUSY

/STATUSO = /BUSY * MMAP * /MDI * MDO * /A1 * A0

;
; Module Status
;

/STATEO = MMAP * /MDI * MDO * A1 * /A0

;
; Speech Module Busy
;

BUSY = /RS + /WS + READY


DESCRIPTION

Glue logic to connect TMS5220C to 8 bit data bus and controlled via memory map.