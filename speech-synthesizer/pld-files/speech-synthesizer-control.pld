GAL22V10
speechctl

CLK   MMAP  MDO   MDI   A0      A1      RESET    READY  INT    NC    NC    GND
OE    NC    NC    BUSY  STATEO  STATUSO STATUSI  DATAA  DATAI  WS    RS    VCC


; Memory Map Lines
;
;   Function | A1 | A0
;  ----------+----+----
;   DATA_IN  |  0 |  0
;   STATUS   |  0 |  1
;   STATE    |  1 |  0



;
; Pin Logic TMS5220C Controller
;

;
; Write Data for External Speech Data
;

; clock in the data bus when memory map is appropriate and
; speech module is not otherwise busy
DATAI.R = MMAP * MDI * /MDO * /A1 * /A0 * /BUSY

; activate WS (active low) on initial data click in and keep
; active when READY is high. Assumes READY will go high sooner
; then the next clock cycle after WS activates.
/WS.R = DATAI * /WS + WS * READY

; output data from data register while WS is active and READY is high.
; When REDY goes low the TMS5220 will have latched in data.
/DATAA = WS

;
; TMS5220C Status Fetch Operation
;


; activate RS (active low) when memory map is appropriate and
; speech module not otherwise busy, and keep active while READY
; is high. Assumes READY will go high within 1 clock cycle.
/RS.R = MMAP * MDI * /MDO * /A1 * A0 * /BUSY + RS * READY

; clock in TMS5220 output to status register when READY goes low
; while RS is active. Assumes READY went high within one clock cycle
; from RS going active. Assumes TMS5220 data out will remain valid
; for suffiecent time to be clocked in after RS deactivates.
STATUSI.R = /READY * RS

; Write status register to data bus when memory map is appropriate
; and speech module not otherwise busy.
/STATUSO = MMAP * /MDI * MDO * /A1 * A0 * /BUSY

;
; Module State
;

; Write module state to data bus when memory map is appropriate,
/STATEO = MMAP * /MDI * MDO * A1 * /A0

; Write status register to data bus when memory map is appropriate
; and speech module not otherwise busy.
;
; Speech Module Busy
;

BUSY = RS + WS + READY


DESCRIPTION

Glue logic to connect TMS5220C to 8 bit data bus and controlled via memory map.