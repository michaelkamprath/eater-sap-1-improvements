GAL22V10
speechctl

CLK  MMAP  MDO  MDI    A0      A1       RESET    READY  IINT   RS     WS      GND
NC   NC    NC   BUSY   STATEO  STATUSI  STATUSO  WSRST   RSRST  WSCLK  RSCLK   VCC


; Memory Map Lines
;
;   Function | A1 | A0
;  ----------+----+----
;   DATA_IN  |  0 |  0
;   STATUS   |  0 |  1
;   STATE    |  1 |  0



;
; Pin Logic TMS5220C Controller
;

;
; Write Data for External Speech Data
;

; The WSCLK pin should go high on the CLK when the conditions to intiate a write
; operation are present. When the clock goes low, the WSCLK should be reset.
WSCLK.R = MMAP * MDI * /MDO * /A1 * /A0 * /BUSY



; READY should go high within 100 ns of WS. That is a 10 MHz clock, which
; is 10 times faster than CLK. WSCLK should be active for long enough to
; prevent WSRST from being activated in the time between WS being activated
; and READY going high. Given that, the WSRST should be activated when
; WSCLK is not active, WS is active, and READY is not active (should have transitioned
; from HIGH to LOW given WS was active)

/WSRST.R = /WSCLK * WS * /READY  + RESET

;
; TMS5220C Status Fetch Operation
;


; activate RS (active low) when memory map is appropriate and
; speech module not otherwise busy, and keep active while READY
; is high. Assumes READY will go high within 1 clock cycle.
RSCLK.R = MMAP * MDI * /MDO * /A1 * A0 * /BUSY

STATUSI.R = /RSCLK * RS * /READY

/RSRST = STATUSI + RESET


; Write status register to data bus when memory map is appropriate
; and speech module not otherwise busy.
 /STATUSO = MMAP * /MDI * MDO * /A1 * A0 * /BUSY * STATEO

;
; Module State
;

; Write module state to data bus when memory map is appropriate,
/STATEO = MMAP * /MDI * MDO * A1 * /A0 * STATUSO

; Write status register to data bus when memory map is appropriate
; and speech module not otherwise busy.
;
; Speech Module Busy
;

BUSY = RS + STATUSI + WS + READY


DESCRIPTION

Glue logic to connect TMS5220 to 8 bit data bus and controlled via memory map.